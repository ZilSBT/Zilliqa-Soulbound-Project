(* SPDX-License-Identifier: MIT *)
scilla_version 0

(***************************************************)
(*               Associated library                *)
(***************************************************)
import BoolUtils ListUtils IntUtils
library SoulBoundToken

type Operation =
| Add
| Sub

(* Global variables *)
let zero_address = 0x0000000000000000000000000000000000000000
let false = False
let true = True
let zero_128 = Uint128 0
let zero = Uint256 0
let one = Uint256 1
let two = Uint256 2

let empty_string = ""
let one_string = "1"
let zero_string = "0"

let add_operation = Add
let sub_operation = Sub
let min_fee_bps = Uint128 1
let max_fee_bps = Uint128 10000

(* Error codes *)
let not_paused_error = Int32 01
let paused_error = Int32 02
let self_error = Int32 03
let not_contract_owner_error = Int32 04 
let not_minted_error = Int32 05 
let not_owner_or_operator_error = Int32 06
let zero_address_destination_error = Int32 07 
let not_contract_ownership_recipient_error = Int32 08
let more_than_one_token_error = Int32 09
let less_than_one_token_error = Int32 10
let empty_uri_error = Int32 11

(* Success codes *)
let mint_success = Int32 12
let burn_success = Int32 13
let changeProfileURI_success = Int32 14
let changeDataURI_success = Int32 15
let earn_achievement_success= Int32 16


(* Library functions *)
let one_msg = 
  fun (msg: Message) => 
    let nil_msg = Nil {Message} in
    Cons {Message} msg nil_msg

let two_msgs =
  fun (msg1: Message) =>
  fun (msg2: Message) =>
    let msgs_tmp = one_msg msg2 in
    Cons {Message} msg1 msgs_tmp

let get_bal =
  fun (maybe_bal: Option Uint256) =>
    match maybe_bal with
    | None => zero
    | Some bal => bal
    end


(* Error exception *)
type Error =
  | NotPausedError
  | PausedError
  | SelfError
  | NotContractOwnerError
  | NotMinterError
  | NotOwnerOrOperatorError
  | ZeroAddressDestinationError
  | NotContractOwnershipRecipientError
  | MoreThanOneTokenError
  | LessThanOneTokenError
  | EmptyURI

let make_error =
  fun (result: Error) =>
    let result_code = 
      match result with
      | NotPausedError                     => Int32 -1
      | PausedError                        => Int32 -2
      | SelfError                          => Int32 -3
      | NotContractOwnerError              => Int32 -4
      | NotMinterError                     => Int32 -5
      | NotOwnerOrOperatorError            => Int32 -6
      | ZeroAddressDestinationError        => Int32 -7
      | NotContractOwnershipRecipientError => Int32 -8
      | MoreThanOneTokenError              => Int32 -9
      | LessThanOneTokenError              => Int32 -10
      | EmptyURI                           => Int32 -11
      end
    in
    { _exception: "Error"; code: result_code }


(***************************************************)
(*             The contract definition             *)
(***************************************************)

contract SoulBoundToken
(
  initial_contract_owner: ByStr20,
  name: String,
  symbol: String
)

(* Contract constraints *)
with
  (* `initial_contract_owner` must not be the zero address *)
  let is_contract_owner_invalid = builtin eq initial_contract_owner zero_address in
  (* `name` must not be an empty string *)
  let is_name_invalid = builtin eq name empty_string in
  (* `symbol` must not be an empty string *)
  let is_symbol_invalid = builtin eq symbol empty_string in
  (* Check if any parameter is invalid *)
  let is_name_or_symbol_invalid = orb is_name_invalid is_symbol_invalid in

  let is_invalid = orb is_contract_owner_invalid is_name_or_symbol_invalid in
  negb is_invalid
=>

(* Mutable fields *)

(* Emergency stop mechanism *)
(* Defaults to False *)
field is_paused: Bool = false

(* Contract Owner *)
(* Defaults to `initial_contract_owner` *)
field contract_owner: ByStr20 = initial_contract_owner

(* Contract ownership recipient *)
(* Defaults to `zero_address` *)
field contract_ownership_recipient: ByStr20 = zero_address

(* The total number of existing tokens *)
field total_supply: Uint256 = Uint256 0

(* Mapping from token owner to their balance  *)
(* Address to Balance*)
field balances: Map ByStr20 Uint256 = Emp ByStr20 Uint256

(* Token URIs *)
(* Address[0] == ProfilePictureURI*)
(* Address[1] == ProfileDataURI*)
(* Address[2] == Achievement *)
field token_uris: Map ByStr20 (Map Uint256 String)
  = Emp ByStr20 (Map Uint256 String)


(* Procedure for sending a message *)
procedure send_message (amount: Uint128, code: Int32)
  msg = {
    _tag: "";
    _recipient: _sender;
    _amount: amount;
    code: code
  };
  msgs = one_msg msg;
  send msgs
end
(* Emit Errors *)
procedure Throw(error: Error)
  e = make_error error;
  throw e
end
procedure RequireContractOwner()
  cur_owner <- contract_owner;
  is_contract_owner = builtin eq cur_owner _sender;
  match is_contract_owner with
  | True => 
  | False =>
    send_message zero_128 not_contract_owner_error;
    error = NotContractOwnerError;
    Throw error
  end
end

procedure RequireNotSelf(address_a: ByStr20, address_b: ByStr20)
  is_self = builtin eq address_a address_b;
  match is_self with
  | False =>
  | True =>
    send_message zero_128 self_error;
    error = SelfError;
    Throw error
  end
end

procedure RequireSelf(address_a: ByStr20, address_b: ByStr20)
  is_self = builtin eq address_a address_b;
  match is_self with
  | False =>
    send_message zero_128 self_error;
    error = SelfError;
    Throw error
  | True =>
  end
end

procedure RequireNotPaused()
  (* Reference: *)
  (* https://consensys.github.io/smart-contract-best-practices/general_philosophy/#prepare-for-failure *)
  paused <- is_paused;
  match paused with
  | False =>
  | True =>
    (* Contract is paused *)
    send_message zero_128 paused_error;
    error = PausedError;
    Throw error
  end
end

(*Increments/Decrements address's balance depending on op *)
procedure UpdateBalance(operation: Operation, address: ByStr20)
  match operation with
  | Add =>
    maybe_count <- balances[address];
    new_count = 
      let cur_count = get_bal maybe_count in
      (* if overflow occurs, it throws CALL_CONTRACT_FAILED *)
      builtin add cur_count one;
    balances[address] := new_count
  | Sub =>
    maybe_count <- balances[address];
    new_count = 
      let cur_count = get_bal maybe_count in
      (* if underflow occurs, it throws CALL_CONTRACT_FAILED *)
      builtin sub cur_count one;
    balances[address] := new_count
  end
end

(* Set URI for a given address *)
procedure SetURI(to: ByStr20, picture_uri: String , data_uri: String)
  is_profile_empty_string = builtin eq picture_uri empty_string;
  is_data_empty_string = builtin eq data_uri empty_string;
  is_empty_string = orb is_profile_empty_string is_data_empty_string;
  match is_empty_string with 
  | True => 
    (* noop *)
    send_message zero_128 empty_uri_error;
    error = EmptyURI;
    Throw error
  | False =>
    token_uris[to][zero] := picture_uri;
    token_uris[to][one] := data_uri
  end
end

procedure RequireValidDestination(to: ByStr20)
  (* Reference: https://github.com/ConsenSys/smart-contract-best-practices/blob/master/docs/tokens.md *)
  is_zero_address = builtin eq to zero_address;
  match is_zero_address with
  | False =>
  | True =>
    send_message zero_128 zero_address_destination_error;
    error = ZeroAddressDestinationError;
    Throw error
  end
end

(* Verifies user doesnt already have token  *)
procedure HasNoToken(to: ByStr20)
  tokenbalance <- balances[to];
  balance = get_bal tokenbalance;
  owns_token = builtin eq balance zero;
  match owns_token with
  | False =>
    send_message zero_128 more_than_one_token_error; 
    error = MoreThanOneTokenError;
    Throw error
  | True =>
    (* noop *)
  end
end

(* Verifies user has a token to burn  *)
procedure HasToken(to: ByStr20)
  tokenbalance <- balances[to];
  balance = get_bal tokenbalance;
  owns_token = builtin eq balance zero;
  match owns_token with
  | False => 
    (* noop *)
    | True =>
    send_message zero_128 less_than_one_token_error;
    error = LessThanOneTokenError;
    Throw error
  end
end

(* @Requirements: *)
(* - `to` must not be the zero address. Otherwise, it must throw `ZeroAddressDestinationError` *)
procedure MintToken(to: ByStr20)
 (*Check that address is valid*)
  RequireValidDestination to;
 (*Check that address doesnt have sbt*)
  HasNoToken to;

 (* add one to the total supply *)
  current_supply <- total_supply;
  new_supply = builtin add current_supply one;
  total_supply := new_supply;
  token_id = new_supply;

  (* add one to the token owner balance *)
  UpdateBalance add_operation to
  
end

procedure BurnToken(to: ByStr20)
  (* Destroy existing token *)
  delete token_uris[to][zero];
  delete token_uris[to][one];
  delete token_uris[to][two];
  delete token_uris[to];

  (* subtract one from the balance *)
  UpdateBalance sub_operation to;
  (* Delete the addresses balance (likely redundant) *)
  delete balances[to];
  (* subtract one from the total supply *)
  current_supply <- total_supply;
  new_supply = builtin sub current_supply one;
  total_supply := new_supply;

  e = {
    _eventname: "Burn";
    token_owner: to;
    total_supply: new_supply
  };
  event e
end

(* Mints a token with a specific `picture_uri` & `data_uri` and transfers it to `to`. *)
(* @param: to - Address of the token recipient *)
(* @param: picture_uri - URI of a profile picture *)
(* @param: data_uri - URI of a json that contains users data *)
(* @Requirements: *)
(* - The contract must not be paused. Otherwise, it must throw `PausedError` *)
(* - The URI params mustn't be empty strings . Otherwise , it will throw 'EmptyString' error *)
transition Mint(to: ByStr20, picture_uri: String, data_uri: String)
  RequireNotPaused;
  RequireSelf to _sender;
  MintToken to; 
  SetURI to picture_uri data_uri;
  token_uris[to][two]:=zero_string;
  totalsupply_at_mint <- total_supply; 
  e = {
    _eventname: "Mint";
    to: to;
    totalsupply_at_mint: totalsupply_at_mint;
    picture_uri: picture_uri;
    data_uri: data_uri
  };
  event e;
  
  send_message zero_128 mint_success
 
end


(* @param: to  - The address of the burner  *)
(* @Requirements: *)
(* - The contract must not be paused. Otherwise, it must throw `PausedError` *)
(* - The contract must be called by the address that is passed in as Parameter *)
transition Burn(to: ByStr20)
  RequireNotPaused;
  (*Check that address is valid*)
  RequireValidDestination to;
  RequireSelf to _sender;
  
  (*Check that address has sbt*)
  HasToken to;
  BurnToken to;

  send_message zero_128 burn_success
end


(* @param: to  - The address of the profile  *)
(* @param: picture_uri  - URI of the new picture  *)
(* @Requirements: *)
(* - The contract must not be paused. Otherwise, it must throw `PausedError` *)
(* - The contract must be called by the address that is passed in as Parameter *)
transition ChangeProfilePicture(to: ByStr20 , picture_uri: String)
  RequireNotPaused;
  (*Check that address is valid*)
  RequireValidDestination to;
  RequireSelf to _sender;
  (*Check that address has sbt*)
  HasToken to;
  token_uris[to][zero] := picture_uri;
  send_message zero_128 changeProfileURI_success
end

(* @param: to  - The address of the profile  *)
(* @param: picture_uri  - URI of the new picture  *)
(* @Requirements: *)
(* - The contract must not be paused. Otherwise, it must throw `PausedError` *)
(* - The contract must be called by the address that is passed in as Parameter *)
transition ChangeData(to: ByStr20 , data_uri: String)
  RequireNotPaused;
  (*Check that address is valid*)
  RequireValidDestination to;
  RequireSelf to _sender;
  (*Check that address has sbt*)
  HasToken to;
  token_uris[to][one] := data_uri;
  send_message zero_128 changeDataURI_success
end

(* @param: to  - The address of the profile  *)
(* @param: value  - Bool for achievement   *)
(* @Requirements: *)
(* - The contract must not be paused. Otherwise, it must throw `PausedError` *)
(* - The contract must be called by the address that is passed in as Parameter *)
transition Earn_achievement(to: ByStr20 , value: Uint256)
  RequireNotPaused;
  (*Check that address is valid*)
  RequireValidDestination to;
  RequireNotSelf to _sender;
  (*Check that address has sbt*)
  HasToken to;

  is_earned = builtin eq value one;
  match is_earned with
  | False =>    
    
  | True =>
    token_uris[to][two]:= one_string;
    send_message zero_128 earn_achievement_success
  end
end


(* Pauses the contract. Use this when things are going wrong ('circuit breaker'). *)
(* @Requirements: *)
(* - The contract must not be paused. Otherwise, it must throw `PausedError` *)
(* - `_sender` must be the contract owner. Otherwise, it must throw `NotContractOwnerError` *)
transition Pause()
  RequireNotPaused;
  (* RequireContractOwner;  Maybe add this later.  *)

  is_paused := true;
  e = {
    _eventname: "Pause";
    is_paused: true
  };
  event e;
  msg_to_sender = {
    _tag: "ZSBT_PauseCallback";
    _recipient: _sender;
    _amount: Uint128 0;
    is_paused: true
  };
  msgs = one_msg msg_to_sender;
  send msgs
end

(* Unpauses the contract. *)
(* @Requirements: *)
(* - The contract must be paused. Otherwise, it must throw `NotPausedError` *)
(* - `_sender` must be the contract owner. Otherwise, it must throw `NotContractOwnerError` *)
transition Unpause()

  paused <- is_paused;
  match paused with
  | True =>
  | False =>
    send_message zero_128 not_paused_error;
    error = NotPausedError;
    Throw error
  end;
  (* RequireContractOwner; *)

  is_paused := false;
  e = {
    _eventname: "Unpause";
    is_paused: false
  };
  event e;
  msg_to_sender = {
    _tag: "ZSBT_UnpauseCallback";
    _recipient: _sender;
    _amount: Uint128 0;
    is_paused: false
  };
  msgs = one_msg msg_to_sender;
  send msgs
end

(* Sets `to` as the contract ownership recipient. *)
(* To reset `contract_ownership_recipient`, use `zero_address`. *)
(* i.e., `0x0000000000000000000000000000000000000000` *)
(* @param: to - Address of contract ownership recipient *)
(* @Requirements: *)
(* - `_sender` must be the contract owner. Otherwise, it must throw `NotContractOwnerError` *)
(* - `_sender` must not be `to`. Otherwise, it must throw `SelfError` *)
transition SetContractOwnershipRecipient(to: ByStr20)
  RequireContractOwner;
  RequireNotSelf to _sender;

  contract_ownership_recipient := to;

  e = {
    _eventname: "SetContractOwnershipRecipient";
    to: to
  };
  event e;
  msg_to_sender = {
    _tag: "ZSBT_SetContractOwnershipRecipientCallback";
    _recipient: _sender;
    _amount: Uint128 0;
    to: to
  };
  msgs = one_msg msg_to_sender;
  send msgs
end

(* Sets `contract_ownership_recipient` as the contract owner. *)
(* @Requirements: *)
(* - `_sender` must be the contract ownership recipient. Otherwise, it must throw `NotContractOwnershipRecipientError` *)
transition AcceptContractOwnership()
  recipient <- contract_ownership_recipient;

  is_recipient = builtin eq _sender recipient;
  match is_recipient with
  | False =>
    send_message zero_128 not_contract_ownership_recipient_error;
    error = NotContractOwnershipRecipientError;
    Throw error
  | True =>
    contract_owner := _sender;
    contract_ownership_recipient := zero_address;

    e = {
      _eventname: "AcceptContractOwnership";
      contract_owner: _sender
    };
    event e;
    msg_to_sender = {
      _tag: "ZSBT_AcceptContractOwnershipCallback";
      _recipient: _sender;
      _amount: Uint128 0;
      contract_owner: _sender
    };
    msgs = one_msg msg_to_sender;
    send msgs
  end
end